T 56 K [Адрес загрузки]

[adresses]
G K [Директива, фиксация начального адреса]
T 45 K [Установка адреса для параметров далее]
P 256 F [45 = H, тут будет адрес первого элемента массива]
P 255 F [46 = N, длина массива]
TZ  [востановления адреса целевой ячейки]

[sub]
G K  [Директива, фиксация начального адреса]
[далее в квадратных скобах адреса @ ]
[0] A 3 F [Пролог: формирование кода инструкции возврата]
[1] T [return]35 @   [Пролог: запись инструкции возврата]
[2] A 0 H  [adr = H]
[3] U [adr1]38 @
[4] A 0 N
[5] S [two]2 F
[6] T [adr2]39 @
[7] [START] T 41[trash] @     [self-modified code]
[8]         A [AOF]36 @
[9]         A [adr1]38 @
[10]        T [A1]20 @
[11]        A [AOF]36 @
[12]        A [adr2]39 @
[13]        T [A2]22 @
[14]        A [TOF]37 @
[15]        A [adr1]38 @
[16]        T [T1]23 @
[17]        A [TOF]37 @
[18]        A [adr2]39 @
[19]        T [T2]25 @      [/self-modified code]
[20]        [A1] A 0 F [swapping]
[21]             T [temp]40 @
[22]        [A2] A 0 F
[23]        [T1] T 0 F
[24]             A [temp]40 @
[25]        [T2] T 0 F   [/swapping]
[26]        A [adr1]38 @  [меняем адрес первого числа]
[27]        A [two]2 F
[28]        T [adr1]38 @
[29]        A [adr2]39 @  [меняем адрес второго числа]
[30]        S [two]2  F
[31]        U [adr2]39 @
[32]        S [adr1]38 @
[33]        E [START]7 @
[34] [EXIT]   T [trash]41 @
[35] [return] E 0 F [Эпилог: возвращение из функции]
[CONSTS]
[36]  [A0F]  A 0 F
[37]  [TOF]  T 0 F
[38] [adr1]  P 0 F
[39] [adr2]  P 0 F
[40] [temp]  P 0 F
[41] [trash] P 0 F

[test]
G K
[0] X 0 F
[1] A [adr]8 @
[2] T 256 F [Записываем адрес первого элемента в H]
[3] A [length]9 @
[4] T 255 F [Записываем длину массива в N]
[5] A 5 @
[6] G [sub]56 F  [subprogram]
[7] E 23 @ [Переходим во второй тест]
[8]  [adr]    P 13 @
[9]  [length] P 10 F
[10] [space]  P 0 F
P 0 F
P 0 F
[13] [array] P 1023 F
P 511 F
P 255 F
P 127 F
P 63 F
P 31 F
P 15 F
P 7 F
P 3 F
P 1 F
[23] A [adr2]30 @
[24] T 256 F [Записываем адрес первого элемента в H]
[25] A [length2]31 @
[26] T 255 F [Записываем длину массива в N]
[27] A 27 @
[28] G [sub] 56 F
[29] Z 0 F [STOP]
[30] [adr2] P 33 @
[31] [length2] P 8 F
P 0 F [space]
P 7 F [array]
P 31 F
P 1 F
P 255 F
P 127 F
P 1023 F
P 15 F
P 4095 F

EZ PF
